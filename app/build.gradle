apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

allprojects {
    repositories {
        google()
        maven { url 'https://jitpack.io' }
        maven { url "http://dl.bintray.com/waynejo/maven" }
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "net.samvankooten.finnstickers"
        minSdkVersion 21
        targetSdkVersion 28
        versionName '2.2.5'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        versionCode 35
        ndk {
            /*
             * Sceneform is available for the following ABIs:
             * 'arm64-v8a', 'armeabi-v7a', 'x86_64' and 'x86'.
             * Your application should include the ABIs most appropriate to
             * minimize APK size. Listing 'arm64-v8a' is recommended.
             *
             * The sample app includes two ABIs:
             * 1. 'arm64-v8a' to run on devices
             * 2. 'x86' to run in the Android emulator
             *
             * Using App Bundles means each phone gets only its own ABI, so we can
             * aim for compatibility rather than apk size
             */
            abiFilters 'arm64-v8a', 'x86', 'armeabi-v7a', 'x86_64'
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        debug {
            versionNameSuffix "-dev"
        }
        noMinify {
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        minifyDebug {
            debuggable true
            jniDebuggable true
            renderscriptDebuggable true
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    productFlavors {
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.exifinterface:exifinterface:1.0.0'
    implementation 'androidx.core:core:1.0.2'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.preference:preference:1.1.0-rc01'
    implementation 'com.google.android.material:material:1.0.0'

    implementation 'com.google.firebase:firebase-core:17.0.0'
    implementation 'com.google.firebase:firebase-appindexing:19.0.0'
    implementation 'com.google.firebase:firebase-messaging:19.0.1'

    testImplementation 'junit:junit:4.12'

    //for lifecycle and LiveData and ViewModel
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:2.0.0"

    // Provides ARCore Session and related resources.
    implementation 'com.google.ar:core:1.10.0'
    // Provides ArFragment, and other UX resources.
    implementation 'com.google.ar.sceneform.ux:sceneform-ux:1.10.0'

    implementation 'com.squareup.okhttp3:okhttp:3.13.1'

    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.9.0'

//    implementation 'com.github.stfalcon:stfalcon-imageviewer:0.1.0'
    implementation 'com.github.svank:StfalconImageViewer:master-SNAPSHOT'

    // This is a dependency of StFalconImageViewer. After a gradle update, listing this here
    // seems to be necessary or else the PhotoView class can't be found. (paste shruggie here)
    implementation 'com.github.chrisbanes:PhotoView:2.2.0'

    implementation 'com.github.AppIntro:AppIntro:v5.1.0'

    implementation 'com.google.android.exoplayer:exoplayer:2.9.6'

    implementation 'com.waynejo:androidndkgif:0.3.3'

    implementation 'com.rarepebble:colorpicker:2.4.2'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.ar.sceneform.plugin'

sceneform.asset('sampledata/cowwy_low_poly.obj',
        'default',
        'sampledata/cowwy_low_poly.sfa',
        'src/main/assets/cowwy_low_poly')

sceneform.asset('sampledata/finn_low_poly.obj',
        'default',
        'sampledata/finn_low_poly.sfa',
        'src/main/assets/finn_low_poly')
